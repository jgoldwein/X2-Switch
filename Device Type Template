/**
 *  Modified SmartSense (PEQ) Open/Closed Sensor to behave like TWO x 3-WAY on/off toggle switches 
 *    Requires this device type linked to X2-Switch: Modded 2-Switch App 
 *
 *  Copyright 2014 SmartThings and 2015 - Joel Goldwein
 *  Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except
 *  in compliance with the License. You may obtain a copy of the License at:
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software distributed under the License is distributed
 *  on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License
 *  for the specific language governing permissions and limitations under the License.
 *
 *	This Device Type uses a single modified (hacked) SmartSense door open/close sensor wired to both an SPDT (3-way) 
 *  switch and a standard SPST wall switch, each of whicn can toggle on and off a set of GE Link Lights or similar 
 *  lights or switches.   
 *
 *  The intent is to have a simple and inexpensive battery operated replacement for a standard wall switch to 
 *  operate multiple, 3-way zigbee or zwave lights or switches as well as a one way light or switch using two
 *  separate standard switches in an existing gang-box.
 * 
 *  The SmartSense (PEQ) sensor has been on sale intermittently at Best Buy for under $20, and a 3-way SPDT plus a
 *  standard wall switch costs a few dollars at any hardware store, so you can put the whole thing together for 
 *  about $25.00.
 * 
 *  The SmartSense door open/close sensor can be easily modded and wired to the 3-way wall switch and the standard
 *  wall switch as follows:
 * 
 *    1. Replace (desolder) the reed relay with 3 wires connected to a 3-way wall switch.  
 *    2. Replace (desolder) the small push button switch with 2 wires connected to a standard on/off wall switch. 
 *    3. Provision the "Thing" (make sure you put the standard switch into OFF mode since ON mode at battery installation
 *       causes it to reset), and replace the device type with:
 *       "X2-Switch: Modded SmartSense Open/Closed Sensor/Switch: TWO Switch Version"
 *    4. In selected gang box, have a certified electrician wire the GE link lights that will be used to remain ON
 *       by removing the existing switches and tying together the now hanging On-Off wires.
 *       Cap any "red" wires or otherwise exposed wires.
 *    5. Install the new switches and Thing into the gang box or a new gang box (use standard electrical safety measures). 
 *    6. Use the "X2-Switch Control: 2-Switch Control for Modded SmartSense Open/Closed Sensor/Switch for Multiple Lights"
 *       App to control the lights 
 *
 *
 */
 
metadata {
	definition (name: "X2-Switch: Modded SmartSense Open/Closed Sensor/Switch: TWO Switch Version", namespace: "smartthings", author: "SmartThings") {
    	capability "Battery"
	capability "Configuration"
        capability "Contact Sensor"
	capability "Refresh"
	capability "Temperature Measurement"
      
        command "enrollResponse"
 
 
	fingerprint inClusters: "0000,0001,0003,0402,0500,0020,0B05", outClusters: "0019", manufacturer: "CentraLite", model: "3300-S"
        fingerprint inClusters: "0000,0001,0003,0402,0500,0020,0B05", outClusters: "0019", manufacturer: "CentraLite", model: "3300"
	}
 
	simulator {
 
	}

	preferences {
		input description: "This feature allows you to correct any temperature variations by selecting an offset. Ex: If your sensor consistently reports a temp that's 5 degrees too warm, you'd enter \"-5\". If 3 degrees too cold, enter \"+3\".", displayDuringSetup: false, type: "paragraph", element: "paragraph"
		input "tempOffset", "number", title: "Temperature Offset", description: "Adjust temperature by this many degrees", range: "*..*", displayDuringSetup: false
	}
 
	tiles {
    	standardTile("contact1", "device.contact", width: 2, height: 2) {
			state("open Off", label:"DOWN/OFF", icon:"st.Home.home30", backgroundColor:"#C7B4FA")
			state("open On", label:"DOWN/ON", icon:"st.Home.home30", backgroundColor:"#C7B4FA")
			state("closed Off", label:"UP/OFF", icon:"st.Home.home30", backgroundColor:"#2E5FF2")
			state("closed On", label:"UP/ON", icon:"st.Home.home30", backgroundColor:"#2E5FF2")
		}

		standardTile("contact2", "device.contact", width: 2, height: 2) {
			state("open Off", label:"DOWN/OFF", icon:"st.Home.home30", backgroundColor:"#FFFFFF")
			state("closed Off", label:"UP/OFF", icon:"st.Home.home30", backgroundColor:"#FFFFFF")
			state("open On", label:"DOWN/ON", icon:"st.Home.home30", backgroundColor:"#CC5AF2")
			state("closed On", label:"UP/ON", icon:"st.Home.home30", backgroundColor:"#CC5AF2")
		}

		valueTile("temperature", "device.temperature", inactiveLabel: false) {
			state "temperature", label:'${currentValue}Â°',
				backgroundColors:[
					[value: 31, color: "#153591"],
					[value: 44, color: "#1e9cbb"],
					[value: 59, color: "#90d2a7"],
					[value: 74, color: "#44b621"],
					[value: 84, color: "#f1d801"],
					[value: 95, color: "#d04e00"],
					[value: 96, color: "#bc2323"]
				]
		}
		valueTile("battery", "device.battery", decoration: "flat", inactiveLabel: false) {
			state "battery", label:'${currentValue}% battery', unit:""
		}
        
        standardTile("refresh", "device.refresh", inactiveLabel: false, decoration: "flat") {
			state "default", action:"refresh.refresh", icon:"st.secondary.refresh"
		}
 
		main (["contact1","contact2","temperature"])
        details(["contact1","contact2","temperature","battery","refresh"])
	}
}
 
def parse(String description) {
	log.debug "JWG: Main description: $description"
	Map map = [:]
	if (description?.startsWith('catchall:')) {
		map = parseCatchAllMessage(description)
	}
	else if (description?.startsWith('read attr -')) {
		map = parseReportAttributeMessage(description)
	}
	else if (description?.startsWith('temperature: ')) {
		map = parseCustomMessage(description)
	}
    else if (description?.startsWith('zone status')) {
    	map = parseIasMessage(description)
    }
	log.debug "Parse returned $map"
	def result = map ? createEvent(map) : null
    if (description?.startsWith('enroll request')) {
    	List cmds = enrollResponse()
        log.debug "enroll response: ${cmds}"
        result = cmds?.collect { new physicalgraph.device.HubAction(it) }
    }
    return result
}
 
private Map parseCatchAllMessage(String description) {
	log.debug "description A: $description"
	Map resultMap = [:]
    def cluster = zigbee.parse(description)
    if (shouldProcessMessage(cluster)) {
        switch(cluster.clusterId) {
            case 0x0001:
            	resultMap = getBatteryResult(cluster.data.last())
                break

            case 0x0402:
				// log.debug 'TEMP'
                // temp is last 2 data values. reverse to swap endian
                String temp = cluster.data[-2..-1].reverse().collect { cluster.hex1(it) }.join()
                def value = getTemperature(temp)
                resultMap = getTemperatureResult(value)
                break
        }
    }

    return resultMap
}

private boolean shouldProcessMessage(cluster) {
    // 0x0B is default response indicating message got through
    // 0x07 is bind message
    boolean ignoredMessage = cluster.profileId != 0x0104 || 
        cluster.command == 0x0B ||
        cluster.command == 0x07 ||
        (cluster.data.size() > 0 && cluster.data.first() == 0x3e)
    return !ignoredMessage
}

private int getHumidity(value) {
    return Math.round(Double.parseDouble(value))
}
 
private Map parseReportAttributeMessage(String description) {
	Map descMap = (description - "read attr - ").split(",").inject([:]) { map, param ->
		def nameAndValue = param.split(":")
		map += [(nameAndValue[0].trim()):nameAndValue[1].trim()]
	}
	log.debug "Desc Map: $descMap"
	Map resultMap = [:]
	if (descMap.cluster == "0402" && descMap.attrId == "0000") {
		def value = getTemperature(descMap.value)
		resultMap = getTemperatureResult(value)
	}
	else if (descMap.cluster == "0001" && descMap.attrId == "0020") {
		resultMap = getBatteryResult(Integer.parseInt(descMap.value, 16))
	}
 
	return resultMap
}
 
private Map parseCustomMessage(String description) {
	log.debug "description B: $description"
	Map resultMap = [:]
	if (description?.startsWith('temperature: ')) {
		def value = zigbee.parseHATemperatureValue(description, "temperature: ", getTemperatureScale())
		resultMap = getTemperatureResult(value)
	}
	return resultMap
}

private Map parseIasMessage(String description) {
    List parsedMsg = description.split(' ')
    String msgCode = parsedMsg[2]
	log.debug "Switch msgCode: $msgCode"     
    Map resultMap = [:]
    switch(msgCode) {
        case '0x0020': // Closed/No Motion/Dry/On/Up (cover on)
        	resultMap = getContactResult('closed On')
            break

        case '0x0021': // Open/Motion/Wet/off/down (cover on)
        	resultMap = getContactResult('open On')
            break

        case '0x0022': // Tamper Alarm
            break

        case '0x0023': // Battery Alarm
            break

        case '0x0024': // Closed/No Motion/Dry/On/Up (cover off)
        	resultMap = getContactResult('closed Off')
            break

        case '0x0025': // Open/Motion/Wet/off/down (cover off)
        	resultMap = getContactResult('open Off')
            break

        case '0x0026': // Trouble/Failure
            break

        case '0x0028': // Test Mode
            break
    }
    return resultMap
}
 
def getTemperature(value) {
	def celsius = Integer.parseInt(value, 16).shortValue() / 100
	if(getTemperatureScale() == "C"){
		return celsius
	} else {
		return celsiusToFahrenheit(celsius) as Integer
	}
}

private Map getBatteryResult(rawValue) {
//	log.debug 'Battery'
	def linkText = getLinkText(device)
    
    def result = [
    	name: 'battery'
    ]
    
	def volts = rawValue / 10
	def descriptionText
	if (volts > 3.5) {
		result.descriptionText = "${linkText} battery has too much power (${volts} volts)."
	}
	else {
		def minVolts = 2.1
    	def maxVolts = 3.0
		def pct = (volts - minVolts) / (maxVolts - minVolts)
		result.value = Math.min(100, (int) pct * 100)
		result.descriptionText = "${linkText} battery was ${result.value}%"
	}

	return result
}

private Map getTemperatureResult(value) {
//	log.debug 'TEMP'
	def linkText = getLinkText(device)
	if (tempOffset) {
		def offset = tempOffset as int
		def v = value as int
		value = v + offset
	}
	def descriptionText = "${linkText} was ${value}Â°${temperatureScale}"
	return [
		name: 'temperature',
		value: value,
		descriptionText: descriptionText
	]
}

private Map getContactResult(value) {
	def linkText = getLinkText(device)
  	def descriptionText = "${linkText} was ${value == 'openOff' ? 'Down and Off' : 'closed'}"
//    log.debug "linkText: $descriptionText"
	return [
		name: 'contact',
		value: value,
		descriptionText: descriptionText
	]
}

def refresh()
{
	log.debug "Refreshing Temperature and Battery"
	[
    
        "st rattr 0x${device.deviceNetworkId} 1 0x402 0", "delay 200",
		"st rattr 0x${device.deviceNetworkId} 1 1 0x20"

	]
}

def configure() {

	String zigbeeId = swapEndianHex(device.hub.zigbeeId)
	log.debug "Confuguring Reporting, IAS CIE, and Bindings."
	def configCmds = [
		"zcl global write 0x500 0x10 0xf0 {${zigbeeId}}", "delay 200",
		"send 0x${device.deviceNetworkId} 1 1", "delay 1500",
        
        "zcl global send-me-a-report 1 0x20 0x20 600 3600 {01}", "delay 200",
        "send 0x${device.deviceNetworkId} 1 1", "delay 1500",
        
        "zcl global send-me-a-report 0x402 0 0x29 300 3600 {6400}", "delay 200",
        "send 0x${device.deviceNetworkId} 1 1", "delay 1500",
        
        
        //"raw 0x500 {01 23 00 00 00}", "delay 200",
        //"send 0x${device.deviceNetworkId} 1 1", "delay 1500",
        
        
		"zdo bind 0x${device.deviceNetworkId} 1 1 0x402 {${device.zigbeeId}} {}", "delay 500",
		"zdo bind 0x${device.deviceNetworkId} 1 1 1 {${device.zigbeeId}} {}"
	]
    return configCmds + refresh() // send refresh cmds as part of config
}

def enrollResponse() {
	log.debug "Sending enroll response"
    [	
    	
	"raw 0x500 {01 23 00 00 00}", "delay 200",
    "send 0x${device.deviceNetworkId} 1 1"
        
    ]
}
private hex(value) {
	new BigInteger(Math.round(value).toString()).toString(16)
}

private String swapEndianHex(String hex) {
    reverseArray(hex.decodeHex()).encodeHex()
}

private byte[] reverseArray(byte[] array) {
    int i = 0;
    int j = array.length - 1;
    byte tmp;
    while (j > i) {
        tmp = array[j];
        array[j] = array[i];
        array[i] = tmp;
        j--;
        i++;
    }
    return array
}
